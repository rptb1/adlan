                           (ADLAN FRAMEWORK FILE)

OPTIONS
  2; 0; <You can't go that way>; <^L>;
ENDOPTIONS

MESSAGES
  $1    <Ok.>;
  $2    <I can't see any about.>;
  $3    <You don't have any.>;
ENDMESSAGES

WORDS
  north/n; south/s; east/e; west/w;
  up/u;
  down/d;

  take/get/grab/sieze;
  pick;          (pick up)

  drop/leave;
  put;           (put down)

  inventory/i/carry;
  look/l;
  examine; at;   (look at)

  quit/stop;
ENDWORDS

OBJECTS
  object,,,<test object>,<test obj desc>;
ENDOBJECTS

PLACES [s]
  s,s2---,object,<test description>,{},{};
  s2,x1 s --,,<another test>,{},{};
  x1,s s2 --,,<yet another>,{},{};
  carry,----,,<>,{},{};
ENDPLACES

BEFORE
{
  cr
  if both describing and count here
  {
     print <\You can also see:\>
     list here cr
  }
  print <^P^X*^X >
}

AFTER
{
  if typed quit
  {
     print <Are you sure you want to quit? (Y/N)>
     if yesno { quit }
     done
  }

  if typed [take/pick up]
  {
     if find item 1 at carry
     {
        print <You've already got that.> done
     }
     if not find item 1 at here
     {
        print $2 done
     }
     move item 1 from here to carry
     print $1 done
  }

  if typed [drop/put down]
  {
     if not find item 1 at carry
     {
        print $3 done
     }
     move item 1 from carry to here
     print $1 done
  }

  if typed inventory
  {
     if count carry
     {
        print <You're carrying:\> list carry done
     }
     print <You aren't carrying anything.> done
  }

  if typed [examine/look at]
  {
     if not find item 1 at here/carry
     {
        print <There's no more detail on that here.> done
     }
     examine item 1
     done
  }

  if typed look { describe done }

  print <I don't understand.>
}
